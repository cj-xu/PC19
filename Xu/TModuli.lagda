
  ============================================================
  =                                                          =
  =  T-definable continuity-moduli of T-definable functions  =
  =                                                          =
  ============================================================

                       Chuangjie Xu

     Proof and Computation, 20-26 Sep 2019, Herrsching


We combine the construction of moduli of continuity with the
b-translation, similarly to the construction of general-bar-recursion
functionals in [OS18].  For this, we have to extend system T with
cartesian products.  In specific, natural numbers are interpreted as
pairs of functions (â„• â†’ â„•) â†’ â„•.  The idea is that the second component
of such a pair is a modulus of continuity of the first component.
Working with a parametrized logical relation and a predicate of
continuity, we show that every T-definable function (â„• â†’ â„•) â†’ â„• has a
T-definable modulus of continuity.


References.

[Esc13] M. H. EscardÃ³.  Continuity of GÃ¶delâ€™s system T functionals via
        effectful forcing.  MFPSâ€™2013.  Electronic Notes in
        Theoretical Computer Science, 298:119â€“141, 2013.

[OS18]  P. Oliva and S. Steila.  A direct proof of Schwichtenbergâ€™s bar
        recursion closure theorem.  The Journal of Symbolic Logic,
        83(1):70â€“83, 2018.

\begin{code}

module TModuli where

open import Preliminaries

\end{code}

â–  Extending system T with cartesian products 

\begin{code}

infixr 40 _âŠ _
infixr 30 _â‡¾_
infixl 20 _â‚Š_
infixl 20 _Â·_

--
-- types
--
data Ty : Set where
 Î¹       : Ty
 _âŠ _ _â‡¾_ : Ty â†’ Ty â†’ Ty

--
-- contexts
--
data Cxt : Set where
 Îµ   : Cxt
 _â‚Š_ : Cxt â†’ Ty â†’ Cxt

--
-- indices of types (i.e. variables in context)
--
data âˆ¥_âˆˆ_âˆ¥ (Ïƒ : Ty) : Cxt â†’ Set where
 zero : {Î“ : Cxt} â†’ âˆ¥ Ïƒ âˆˆ (Î“ â‚Š Ïƒ) âˆ¥
 succ : {Ï„ : Ty} {Î“ : Cxt} â†’ âˆ¥ Ïƒ âˆˆ Î“ âˆ¥ â†’ âˆ¥ Ïƒ âˆˆ Î“ â‚Š Ï„ âˆ¥

--
-- terms
--
data T (Î“ : Cxt) : Ty â†’ Set where
 Var  : {Ïƒ : Ty} â†’ âˆ¥ Ïƒ âˆˆ Î“ âˆ¥ â†’ T Î“ Ïƒ
 Lam  : {Ïƒ Ï„ : Ty} â†’ T (Î“ â‚Š Ïƒ) Ï„ â†’ T Î“ (Ïƒ â‡¾ Ï„)
 _Â·_  : {Ïƒ Ï„ : Ty} â†’ T Î“ (Ïƒ â‡¾ Ï„) â†’ T Î“ Ïƒ â†’ T Î“ Ï„
 Pair : {Ïƒ Ï„ : Ty} â†’ T Î“ (Ïƒ â‡¾ Ï„ â‡¾ Ïƒ âŠ  Ï„)
 Pr1  : {Ïƒ Ï„ : Ty} â†’ T Î“ (Ïƒ âŠ  Ï„ â‡¾ Ïƒ)
 Pr2  : {Ïƒ Ï„ : Ty} â†’ T Î“ (Ïƒ âŠ  Ï„ â‡¾ Ï„)
 Zero : T Î“ Î¹
 Succ : T Î“ (Î¹ â‡¾ Î¹)
 Rec  : {Ïƒ : Ty} â†’ T Î“ (Ïƒ â‡¾ (Î¹ â‡¾ Ïƒ â‡¾ Ïƒ) â‡¾ Î¹ â‡¾ Ïƒ)

\end{code}

â–  The standard interpretation of T into Agda

\begin{code}

âŸ¦_âŸ§Ê¸ : Ty â†’ Set
âŸ¦ Î¹ âŸ§Ê¸     = â„•
âŸ¦ Ïƒ âŠ  Ï„ âŸ§Ê¸ = âŸ¦ Ïƒ âŸ§Ê¸ Ã— âŸ¦ Ï„ âŸ§Ê¸
âŸ¦ Ïƒ â‡¾ Ï„ âŸ§Ê¸ = âŸ¦ Ïƒ âŸ§Ê¸ â†’ âŸ¦ Ï„ âŸ§Ê¸

âŸ¦_âŸ§Ë£ : Cxt â†’ Set
âŸ¦ Îµ âŸ§Ë£     = ğŸ™
âŸ¦ Î“ â‚Š Ï âŸ§Ë£ = âŸ¦ Î“ âŸ§Ë£ Ã— âŸ¦ Ï âŸ§Ê¸

_â‚_â‚ : {Î“ : Cxt} {Ï : Ty} â†’ âŸ¦ Î“ âŸ§Ë£ â†’ âˆ¥ Ï âˆˆ Î“ âˆ¥ â†’ âŸ¦ Ï âŸ§Ê¸
(_ , a) â‚ zero â‚   = a
(Î³ , _) â‚ succ i â‚ = Î³ â‚ i â‚

âŸ¦_âŸ§áµ : {Î“ : Cxt} {Ïƒ : Ty} â†’ T Î“ Ïƒ â†’ âŸ¦ Î“ âŸ§Ë£ â†’ âŸ¦ Ïƒ âŸ§Ê¸
âŸ¦ Var i âŸ§áµ Î³ = Î³ â‚ i â‚
âŸ¦ Lam t âŸ§áµ Î³ = Î» a â†’ âŸ¦ t âŸ§áµ (Î³ , a)
âŸ¦ f Â· a âŸ§áµ Î³ = âŸ¦ f âŸ§áµ Î³ (âŸ¦ a âŸ§áµ Î³)
âŸ¦ Pair âŸ§áµ  _ = _,_
âŸ¦ Pr1 âŸ§áµ   _ = prâ‚
âŸ¦ Pr2 âŸ§áµ   _ = prâ‚‚
âŸ¦ Zero âŸ§áµ  _ = 0
âŸ¦ Succ âŸ§áµ  _ = succ
âŸ¦ Rec âŸ§áµ   _ = rec

âŸ¦_âŸ§ : {Ï : Ty} â†’ T Îµ Ï â†’ âŸ¦ Ï âŸ§Ê¸
âŸ¦ t âŸ§ = âŸ¦ t âŸ§áµ â‹†

--
-- An (Agda) element a is "T-definable" if one can find a closed T
-- term whose standard interpretation is a.
--
T-definable : {Ï : Ty} â†’ âŸ¦ Ï âŸ§Ê¸ â†’ Set
T-definable {Ï} a = Î£ \(t : T Îµ Ï) â†’ âŸ¦ t âŸ§ â‰¡ a

\end{code}

â–  Some auxiliaries

\begin{code}

Î½â‚€ : {Î“ : Cxt} {Ï : Ty} â†’ T (Î“ â‚Š Ï) Ï
Î½â‚€ = Var zero

Î½â‚ : {Î“ : Cxt} {Ï Ïƒ : Ty} â†’ T (Î“ â‚Š Ï â‚Š Ïƒ) Ï
Î½â‚ = Var (succ zero)

Î½â‚‚ : {Î“ : Cxt} {Ï Ïƒâ‚€ Ïƒâ‚ : Ty} â†’ T (Î“ â‚Š Ï â‚Š Ïƒâ‚€ â‚Š Ïƒâ‚) Ï
Î½â‚‚ = Var (succ (succ zero))

Î½â‚ƒ : {Î“ : Cxt} {Ï Ïƒâ‚€ Ïƒâ‚ Ïƒâ‚‚ : Ty} â†’ T (Î“ â‚Š Ï â‚Š Ïƒâ‚€ â‚Š Ïƒâ‚ â‚Š Ïƒâ‚‚) Ï
Î½â‚ƒ = Var (succ (succ (succ zero)))

Max : {Î“ : Cxt} â†’ T Î“ (Î¹ â‡¾ Î¹ â‡¾ Î¹)
Max = Rec Â· Lam Î½â‚€ Â· Lam (Lam (Rec Â· (Succ Â· Î½â‚) Â· Lam (Lam (Succ Â· (Î½â‚‚ Â· Î½â‚)))))
   -- rec (Î» n â†’ n) (Î» n f â†’ rec (succ n) (Î» m _ â†’ succ (f m)))

Max-specâ‚€ : (n m : â„•) â†’ n â‰¤ âŸ¦ Max âŸ§ n m
Max-specâ‚€  0        m       = â‰¤zero
Max-specâ‚€ (succ n)  0       = â‰¤refl
Max-specâ‚€ (succ n) (succ m) = â‰¤succ (Max-specâ‚€ n m)

Max-specâ‚ : (n m : â„•) â†’ m â‰¤ âŸ¦ Max âŸ§ n m
Max-specâ‚  0        m       = â‰¤refl
Max-specâ‚ (succ n)  0       = â‰¤zero
Max-specâ‚ (succ n) (succ m) = â‰¤succ (Max-specâ‚ n m)

\end{code}

â–  A syntactic translation of T into itself

A term  w : Î¹áµ‡  denotes a pair  (wâ‚,wâ‚‚)  of functionals (Î¹ â†’ Î¹) â†’ Î¹

 - wâ‚ is the value

 - wâ‚‚ is the modulus of continuity of wâ‚

\begin{code}

âŸ¨_âŸ©áµ‡ : Ty â†’ Ty
âŸ¨ Î¹ âŸ©áµ‡     = ((Î¹ â‡¾ Î¹) â‡¾ Î¹) âŠ  ((Î¹ â‡¾ Î¹) â‡¾ Î¹)
âŸ¨ Ïƒ âŠ  Ï„ âŸ©áµ‡ = âŸ¨ Ïƒ âŸ©áµ‡ âŠ  âŸ¨ Ï„ âŸ©áµ‡
âŸ¨ Ïƒ â‡¾ Ï„ âŸ©áµ‡ = âŸ¨ Ïƒ âŸ©áµ‡ â‡¾ âŸ¨ Ï„ âŸ©áµ‡

âŸª_âŸ«áµ‡ : Cxt â†’ Cxt
âŸª Îµ âŸ«áµ‡     = Îµ
âŸª Î“ â‚Š Ï âŸ«áµ‡ = âŸª Î“ âŸ«áµ‡ â‚Š âŸ¨ Ï âŸ©áµ‡

âŸ¨_âŸ©áµ› : {Î“ : Cxt} {Ï : Ty} â†’ âˆ¥ Ï âˆˆ Î“ âˆ¥ â†’ âˆ¥ âŸ¨ Ï âŸ©áµ‡ âˆˆ âŸª Î“ âŸ«áµ‡ âˆ¥
âŸ¨ zero âŸ©áµ›   = zero
âŸ¨ succ i âŸ©áµ› = succ âŸ¨ i âŸ©áµ›

KEá¶¥ : {Î“ : Cxt} (Ï : Ty)
    â†’ T Î“ ((Î¹ â‡¾ âŸ¨ Ï âŸ©áµ‡) â‡¾ âŸ¨ Î¹ âŸ©áµ‡ â‡¾ âŸ¨ Ï âŸ©áµ‡)
KEá¶¥  Î¹      = Lam (Lam (Pair Â· Lam (Pr1 Â· (Î½â‚‚ Â· (Pr1 Â· Î½â‚ Â· Î½â‚€)) Â· Î½â‚€)
                             Â· Lam (Max Â· (Pr2 Â· (Î½â‚‚ Â· (Pr1 Â· Î½â‚ Â· Î½â‚€)) Â· Î½â‚€) Â· (Pr2 Â· Î½â‚ Â· Î½â‚€))))
           -- Î» g f â†’ ( Î» Î± â†’ (g(fâ‚Î±))â‚Î±  ,  Î» Î± â†’ max ((g(fâ‚Î±))â‚‚Î±) (fâ‚‚Î±) )
KEá¶¥ (Ïƒ âŠ  Ï„) = Lam (Lam (Pair Â· (KEá¶¥ Ïƒ Â· Lam (Pr1 Â· (Î½â‚‚ Â· Î½â‚€)) Â· Î½â‚€)
                             Â· (KEá¶¥ Ï„ Â· Lam (Pr2 Â· (Î½â‚‚ Â· Î½â‚€)) Â· Î½â‚€)))
           -- Î» g f â†’ ( KEá¶¥ (prâ‚ âˆ˜ g) f  ,  KEá¶¥ (prâ‚‚ âˆ˜ g) f )
KEá¶¥ (_ â‡¾ Ï) = Lam (Lam (Lam (KEá¶¥ Ï Â· Lam (Î½â‚ƒ Â· Î½â‚€ Â· Î½â‚) Â· Î½â‚)))
           -- Î» g f x â†’ KEá¶¥ (Î» k â†’ g k x) f

infix 60 _áµ‡

_áµ‡ : {Î“ : Cxt} {Ï : Ty} â†’ T Î“ Ï â†’ T âŸª Î“ âŸ«áµ‡ âŸ¨ Ï âŸ©áµ‡
Var i áµ‡   = Var âŸ¨ i âŸ©áµ›
Lam t áµ‡   = Lam (t áµ‡)
(f Â· a) áµ‡ = f áµ‡ Â· a áµ‡
Pair áµ‡    = Pair
Pr1 áµ‡     = Pr1
Pr2 áµ‡     = Pr2
Zero áµ‡    = Pair Â· Lam Zero Â· Lam Zero
         -- ( Î» Î± â†’ 0  ,  Î» Î± â†’ 0 )
Succ áµ‡    = Lam (Pair Â· Lam (Succ Â· (Pr1 Â· Î½â‚ Â· Î½â‚€)) Â· (Pr2 Â· Î½â‚€))
         -- Î» w â†’ ( succ âˆ˜ wâ‚  ,  wâ‚‚ )
Rec {Ï} áµ‡ = Lam (Lam (KEá¶¥ Ï Â· (Rec Â· Î½â‚ Â· Lam (Î½â‚ Â· (Pair Â· Lam Î½â‚ Â· Lam Zero)))))
         -- Î» a f â†’ KEá¶¥ (rec a (Î» k â†’ f (Î»Î±â†’k , Î»Î±â†’0)))

\end{code}

â–  Generic element Î©

\begin{code}

Î© : {Î“ : Cxt} â†’ T Î“ (âŸ¨ Î¹ âŸ©áµ‡ â‡¾ âŸ¨ Î¹ âŸ©áµ‡)
Î© = Lam (Pair Â· Lam (Î½â‚€ Â· (Pr1 Â· Î½â‚ Â· Î½â‚€)) Â·
                Lam (Max Â· (Pr2 Â· Î½â‚ Â· Î½â‚€) Â· (Succ Â· (Pr1 Â· Î½â‚ Â· Î½â‚€))))
 -- Î» f â†’ ( Î» Î± â†’ Î±(fâ‚Î±)  ,  Î» Î± â†’ max (fâ‚‚Î±) (fâ‚Î± + 1) )

\end{code}

â–  Relating the T terms and their b-translation

To get some "equality" between terms and their translation, we define
a parametrised logical relation R between (the standard interpretations
of) T terms and their b-translation.

\begin{code}

R : {Ï : Ty} â†’ â„•á´º â†’ âŸ¦ âŸ¨ Ï âŸ©áµ‡ âŸ§Ê¸ â†’ âŸ¦ Ï âŸ§Ê¸ â†’ Set
R {Î¹    } Î± f n = prâ‚ f Î± â‰¡ n
R {Ïƒ âŠ  Ï„} Î± f g = R Î± (prâ‚ f) (prâ‚ g) Ã— R Î± (prâ‚‚ f) (prâ‚‚ g)
R {Ïƒ â‡¾ Ï„} Î± f g = {x : âŸ¦ âŸ¨ Ïƒ âŸ©áµ‡ âŸ§Ê¸} {y : âŸ¦ Ïƒ âŸ§Ê¸} â†’ R Î± x y â†’ R Î± (f x) (g y)

--
-- The Kleisli extension preserves R.
--
R[KEá¶¥] : (Ï : Ty) (Î± : â„•á´º) {f : â„• â†’ âŸ¦ âŸ¨ Ï âŸ©áµ‡ âŸ§Ê¸} {g : â„• â†’ âŸ¦ Ï âŸ§Ê¸}
       â†’ ((i : â„•) â†’ R Î± (f i) (g i))
       â†’ {Î“ : Cxt} {Î³ : âŸ¦ Î“ âŸ§Ë£}
       â†’ R Î± (âŸ¦ KEá¶¥ Ï âŸ§áµ Î³ f) g
R[KEá¶¥]  Î¹      Î± Î¶ Ï‡ = trans (Î¶ _) (ap _ Ï‡)
R[KEá¶¥] (Ïƒ âŠ  Ï„) Î± Î¶ Ï‡ = R[KEá¶¥] Ïƒ Î± (prâ‚ âˆ˜ Î¶) Ï‡ , R[KEá¶¥] Ï„ Î± (prâ‚‚ âˆ˜ Î¶) Ï‡
R[KEá¶¥] (_ â‡¾ Ï) Î± Î¶ Ï‡ = Î» Î· â†’ R[KEá¶¥] Ï Î± (Î» i â†’ Î¶ i Î·) Ï‡

--
-- Extending the logical relation to contexts
--
RË£ : {Î“ : Cxt} â†’ â„•á´º â†’ âŸ¦ âŸª Î“ âŸ«áµ‡ âŸ§Ë£ â†’ âŸ¦ Î“ âŸ§Ë£ â†’ Set
RË£ {Îµ}     _  _       _      = ğŸ™
RË£ {Î“ â‚Š Ï} Î± (Î³ , a) (Î¾ , b) = RË£ Î± Î³ Î¾ Ã— R Î± a b

--
-- Variables in related contexts are related.
--
R[Var] : {Î“ : Cxt} {Ï : Ty} {Î³ : âŸ¦ âŸª Î“ âŸ«áµ‡ âŸ§Ë£} {Î¾ : âŸ¦ Î“ âŸ§Ë£} (Î± : â„•á´º)
       â†’ RË£ Î± Î³ Î¾ â†’ (i : âˆ¥ Ï âˆˆ Î“ âˆ¥) â†’ R Î± (Î³ â‚ âŸ¨ i âŸ©áµ› â‚) (Î¾ â‚ i â‚)
R[Var] {Îµ}     Î±  _      ()
R[Var] {Î“ â‚Š Ï} Î± (_ , Î¸)  zero    = Î¸
R[Var] {Î“ â‚Š Ï} Î± (Î¶ , _) (succ i) = R[Var] Î± Î¶ i

--
-- Any T term and its b-translation are related.
--
Lemma[R] : {Î“ : Cxt} {Ï : Ty} (t : T Î“ Ï) (Î± : â„•á´º)
         â†’ {Î³ : âŸ¦ âŸª Î“ âŸ«áµ‡ âŸ§Ë£} {Î¾ : âŸ¦ Î“ âŸ§Ë£} â†’ RË£ Î± Î³ Î¾
         â†’ R Î± (âŸ¦ t áµ‡ âŸ§áµ Î³) (âŸ¦ t âŸ§áµ Î¾)
Lemma[R] (Var i)  Î± Î¶ = R[Var] Î± Î¶ i
Lemma[R] (Lam t)  Î± Î¶ = Î» Ï‡ â†’ Lemma[R] t Î± (Î¶ , Ï‡)
Lemma[R] (f Â· a)  Î± Î¶ = Lemma[R] f Î± Î¶ (Lemma[R] a Î± Î¶)
Lemma[R] Pair     _ _ = Î» Ï‡â‚€ Ï‡â‚ â†’ Ï‡â‚€ , Ï‡â‚
Lemma[R] Pr1      _ _ = prâ‚
Lemma[R] Pr2      _ _ = prâ‚‚
Lemma[R] Zero     _ _ = refl
Lemma[R] Succ     Î± _ = ap succ
Lemma[R] (Rec{Ï}) Î± _ {x} {y} Ï‡ {f} {g} Î· = R[KEá¶¥] Ï Î± claim
 where
  claim : (i : â„•) â†’ R Î± (rec x (Î» k â†’ f ((Î» _ â†’ k) , (Î» _ â†’ 0))) i) (rec y g i)
  claim  0       = Ï‡
  claim (succ i) = Î· refl (claim i)

--
-- In particular, any t : (â„• â†’ â„•) â†’ â„• is pointwise equal to the first
-- component of táµ‡(Î©).
--
Theorem[R] : (t : T Îµ ((Î¹ â‡¾ Î¹) â‡¾ Î¹))
           â†’ (Î± : â„•á´º) â†’ prâ‚ (âŸ¦ t áµ‡ Â· Î© âŸ§) Î± â‰¡ âŸ¦ t âŸ§ Î±
Theorem[R] t Î± = Lemma[R] t Î± â‹† (ap Î±)

\end{code}

â–  A continuity predicate

\begin{code}

_is-a-modulus-of-continuity-of_ : (â„•á´º â†’ â„•) â†’ (â„•á´º â†’ â„•) â†’ Set
M is-a-modulus-of-continuity-of f = (Î± Î² : â„•á´º) â†’ Î± â‰¡[ M Î± ] Î² â†’ f Î± â‰¡ f Î²

C : (Ï : Ty) â†’ âŸ¦ âŸ¨ Ï âŸ©áµ‡ âŸ§Ê¸ â†’ Set
C  Î¹      (f , M) = M is-a-modulus-of-continuity-of f
C (Ïƒ âŠ  Ï„) (f , g) = C Ïƒ f Ã— C Ï„ g
C (Ïƒ â‡¾ Ï„)  f      = {a : âŸ¦ âŸ¨ Ïƒ âŸ©áµ‡ âŸ§Ê¸} â†’ C Ïƒ a â†’ C Ï„ (f a)

--
-- The Kleisli extension preserves the predicate.
--
C[KEá¶¥] : (Ï : Ty)
       â†’ {g : â„• â†’ âŸ¦ âŸ¨ Ï âŸ©áµ‡ âŸ§Ê¸} â†’ ((i : â„•) â†’ C Ï (g i))
       â†’ {Î“ : Cxt} {Î³ : âŸ¦ Î“ âŸ§Ë£}
       â†’ C (Î¹ â‡¾ Ï) (âŸ¦ KEá¶¥ Ï âŸ§áµ Î³ g)
C[KEá¶¥]  Î¹ {g} cg {_} {_} {f , M} cf Î± Î² em = trans eâ‚‚ eâ‚
 where
  eâ‚€ : f Î± â‰¡ f Î²
  eâ‚€ = cf Î± Î² (â‰¡[â‰¤] em (Max-specâ‚ (prâ‚‚ (g (f Î±)) Î±) (M Î±)))
  eâ‚ : prâ‚ (g (f Î±)) Î² â‰¡ prâ‚ (g (f Î²)) Î²
  eâ‚ = ap (Î» i â†’ prâ‚ (g i) Î²) eâ‚€
  eâ‚‚ : prâ‚ (g (f Î±)) Î± â‰¡ prâ‚ (g (f Î±)) Î²
  eâ‚‚ = cg (f Î±) Î± Î² (â‰¡[â‰¤] em (Max-specâ‚€ _ _))
C[KEá¶¥] (Ïƒ âŠ  Ï„) cg cf = C[KEá¶¥] Ïƒ (prâ‚ âˆ˜ cg) cf , C[KEá¶¥] Ï„ (prâ‚‚ âˆ˜ cg) cf
C[KEá¶¥] (_ â‡¾ Ï) cg cf = Î» ca â†’ C[KEá¶¥] Ï (Î» i â†’ cg i ca) cf

--
-- Extending the predicate to contexts
--
CË£ : {Î“ : Cxt} â†’ âŸ¦ âŸª Î“ âŸ«áµ‡ âŸ§Ë£ â†’ Set
CË£ {Îµ}      â‹†      = ğŸ™
CË£ {Î“ â‚Š Ï} (Î³ , a) = CË£ Î³ Ã— C Ï a

--
-- Variables satisfy the predicate.
--
C[Var] : {Î“ : Cxt} {Ï : Ty} {Î³ : âŸ¦ âŸª Î“ âŸ«áµ‡ âŸ§Ë£}
       â†’ CË£ Î³ â†’ (i : âˆ¥ Ï âˆˆ Î“ âˆ¥) â†’ C Ï (Î³ â‚ âŸ¨ i âŸ©áµ› â‚)
C[Var] {Îµ}      _      ()
C[Var] {Î“ â‚Š Ï} (Î´ , Î¸)  zero    = Î¸
C[Var] {Î“ â‚Š Ï} (Î´ , Î¸) (succ i) = C[Var] Î´ i

--
-- The b-translation of any T term satisfies the predicate.
--
Lemma[C] : {Î“ : Cxt} {Ï : Ty} (t : T Î“ Ï)
         â†’ {Î³ : âŸ¦ âŸª Î“ âŸ«áµ‡ âŸ§Ë£} â†’ CË£ Î³ â†’ C Ï (âŸ¦ t áµ‡ âŸ§áµ Î³)
Lemma[C] (Var i)  Î´ = C[Var] Î´ i
Lemma[C] (Lam t)  Î´ = Î» Î¸ â†’ Lemma[C] t (Î´ , Î¸)
Lemma[C] (f Â· a)  Î´ = Lemma[C] f Î´ (Lemma[C] a Î´)
Lemma[C] Pair     _ = Î» Î¸â‚€ Î¸â‚ â†’ Î¸â‚€ , Î¸â‚
Lemma[C] Pr1      _ = prâ‚
Lemma[C] Pr2      _ = prâ‚‚
Lemma[C] Zero     _ = Î» _ _ _ â†’ refl
Lemma[C] Succ     _ = Î» p Î± Î² e â†’ ap succ (p Î± Î² e)
Lemma[C] (Rec{Ï}) _ {a} ca {f} cf = C[KEá¶¥] Ï cr
 where
  cr : (i : â„•) â†’ C Ï (rec a (Î» k â†’ f ((Î» _ â†’ k) , Î» _ â†’ 0)) i)
  cr  0       = ca
  cr (succ i) = cf (Î» _ _ _ â†’ refl) (cr i)

--
-- The generic element also satisfies the predicate.
--
C[Î©] : C (Î¹ â‡¾ Î¹) âŸ¦ Î© âŸ§
C[Î©] {f , M} p Î± Î² em = trans eâ‚‚ eâ‚
 where
  eâ‚€ : f Î± â‰¡ f Î²
  eâ‚€ = p Î± Î² (â‰¡[â‰¤] em (Max-specâ‚€ (M Î±) _))
  eâ‚ : Î² (f Î±) â‰¡ Î² (f Î²)
  eâ‚ = ap Î² eâ‚€
  eâ‚‚ : Î± (f Î±) â‰¡ Î² (f Î±)
  eâ‚‚ = â‰¡[]-< em (Max-specâ‚ (M Î±) _)

\end{code}

â–  Theorem: Every T-definable function (â„• â†’ â„•) â†’ â„• has a T-definable
           modulus of continuity.

\begin{code}

MoC-extensional : {f g M : â„•á´º â†’ â„•}
                â†’ ((Î± : â„•á´º) â†’ f Î± â‰¡ g Î±) â†’ M is-a-modulus-of-continuity-of f
                â†’ M is-a-modulus-of-continuity-of g
MoC-extensional ex cf Î± Î² em = trans (sym (ex Î±)) (trans (cf Î± Î² em) (ex Î²))


Theorem[TModCont] : âˆ€ (f : â„•á´º â†’ â„•) â†’ T-definable f
                    â†’ Î£ \(M : â„•á´º â†’ â„•) â†’ T-definable M
                                      Ã— M is-a-modulus-of-continuity-of f
Theorem[TModCont] f (t , refl) = M , defá´¹ , mcf
 where
  M : â„•á´º â†’ â„•
  M = prâ‚‚ (âŸ¦ t áµ‡ Â· Î© âŸ§)
  defá´¹ : T-definable M
  defá´¹ = Pr2 Â· (t áµ‡ Â· Î©) , refl
  g : â„•á´º â†’ â„•
  g = prâ‚ (âŸ¦ t áµ‡ Â· Î© âŸ§)
  mcg : M is-a-modulus-of-continuity-of g
  mcg = Lemma[C] t â‹† C[Î©]
  ex  : (Î± : â„•á´º) â†’ g Î± â‰¡ f Î±
  ex  = Theorem[R] t
  mcf : M is-a-modulus-of-continuity-of f
  mcf = MoC-extensional ex mcg

\end{code}

â–  Moduli of continuity

\begin{code}

M : T Îµ ((Î¹ â‡¾ Î¹) â‡¾ Î¹) â†’ â„•á´º â†’ â„•
M t = prâ‚ (Theorem[TModCont] âŸ¦ t âŸ§ (t , refl))

M-spec : (t : T Îµ ((Î¹ â‡¾ Î¹) â‡¾ Î¹))
       â†’ (Î± Î² : â„•á´º) â†’ Î± â‰¡[ M t Î± ] Î² â†’ âŸ¦ t âŸ§ Î± â‰¡ âŸ¦ t âŸ§ Î²
M-spec t = prâ‚‚ (prâ‚‚ (Theorem[TModCont] âŸ¦ t âŸ§ (t , refl)))

Mâ±â¿áµ— : T Îµ ((Î¹ â‡¾ Î¹) â‡¾ Î¹) â†’ T Îµ ((Î¹ â‡¾ Î¹) â‡¾ Î¹)
Mâ±â¿áµ— t = prâ‚ (prâ‚ (prâ‚‚ (Theorem[TModCont] âŸ¦ t âŸ§ (t , refl))))

Mâ±â¿áµ—-spec : (t : T Îµ ((Î¹ â‡¾ Î¹) â‡¾ Î¹))
          â†’ (Î± Î² : â„•á´º) â†’ Î± â‰¡[ âŸ¦ Mâ±â¿áµ— t âŸ§ Î± ] Î² â†’ âŸ¦ t âŸ§ Î± â‰¡ âŸ¦ t âŸ§ Î²
Mâ±â¿áµ—-spec = M-spec

\end{code}

â–  Computation experiment

\begin{code}

Num : â„• â†’ {Î“ : Cxt} â†’ T Î“ Î¹
Num  0       = Zero
Num (succ k) = Succ Â· Num k

Plus : {Î“ : Cxt} â†’ T Î“ Î¹ â†’ T Î“ Î¹ â†’ T Î“ Î¹
Plus N M = Rec Â· N Â· Lam Succ Â· M

0Ê· : â„•á´º
0Ê· = 0 Ê·

Î´ : â„•á´º
Î´ i = i

\end{code}

tâ‚€ is a constant function

\begin{code}

tâ‚€ : T Îµ ((Î¹ â‡¾ Î¹) â‡¾ Î¹)
tâ‚€ = Lam (Num 4)

tâ‚€-interpretation : âŸ¦ tâ‚€ âŸ§ â‰¡ Î» Î± â†’ 4
tâ‚€-interpretation = refl

resultâ‚€ :  M tâ‚€ 0Ê· â‰¡ 0
        Ã—  M tâ‚€ Î´ â‰¡ 0
        Ã—  M tâ‚€ succ â‰¡ 0
        Ã—  M tâ‚€ (12 âœ¶ 67 âœ¶ 8 âœ¶ 99 âœ¶ 3 âœ¶ 0Ê·) â‰¡ 0
resultâ‚€ = refl , refl , refl , refl

\end{code}

tâ‚ is a non-trivial constant function: it applies the identity
function Î±â‚â‚‡ (the 18th bit of Î±) times on value 4.

\begin{code}

tâ‚ : T Îµ ((Î¹ â‡¾ Î¹) â‡¾ Î¹)
tâ‚ = Lam (Rec Â· Num 4 Â· Lam (Lam Î½â‚€) Â· (Î½â‚€ Â· Num 17))

tâ‚-interpretation : âŸ¦ tâ‚ âŸ§ â‰¡ Î» Î± â†’ rec 4 (Î» _ k â†’ k) (Î± 17)
tâ‚-interpretation = refl

resultâ‚ :  M tâ‚ 0Ê· â‰¡ 18
        Ã—  M tâ‚ Î´ â‰¡ 18
        Ã—  M tâ‚ succ â‰¡ 18
        Ã—  M tâ‚ (12 âœ¶ 67 âœ¶ 8 âœ¶ 99 âœ¶ 3 âœ¶ 0Ê·) â‰¡ 18
resultâ‚ = refl , refl , refl , refl

\end{code}

tâ‚‚ is a simple function returning the 18th bit of the input.

\begin{code}

tâ‚‚ : T Îµ ((Î¹ â‡¾ Î¹) â‡¾ Î¹)
tâ‚‚ = Lam (Î½â‚€ Â· Num 17)

tâ‚‚-interpretation : âŸ¦ tâ‚‚ âŸ§ â‰¡ Î» Î± â†’ Î± 17
tâ‚‚-interpretation = refl

resultâ‚‚ :  M tâ‚‚ 0Ê· â‰¡ 18
        Ã—  M tâ‚‚ Î´ â‰¡ 18
        Ã—  M tâ‚‚ succ â‰¡ 18
        Ã—  M tâ‚‚ (12 âœ¶ 67 âœ¶ 8 âœ¶ 99 âœ¶ 3 âœ¶ 0Ê·) â‰¡ 18
resultâ‚‚ = refl , refl , refl , refl

\end{code}

tâ‚ƒ is taken from [OS18]:  Î»Î±.Î±(Î±(â€¦(Î±0)â€¦))  with Î±0 applications of Î±.

\begin{code}

tâ‚ƒ : T Îµ ((Î¹ â‡¾ Î¹) â‡¾ Î¹)
tâ‚ƒ = Lam (Rec Â· Num 0 Â· Lam Î½â‚ Â· (Î½â‚€ Â· Num 0))

tâ‚ƒ-interpretation : âŸ¦ tâ‚ƒ âŸ§ â‰¡ Î» Î± â†’ rec 0 (Î» _ â†’ Î±) (Î± 0)
tâ‚ƒ-interpretation = refl

resultâ‚ƒ :  M tâ‚ƒ 0Ê· â‰¡ 1
        Ã—  M tâ‚ƒ Î´ â‰¡ 1
        Ã—  M tâ‚ƒ succ â‰¡ 1
        Ã—  M tâ‚ƒ (12 âœ¶ 67 âœ¶ 8 âœ¶ 99 âœ¶ 3 âœ¶ 0Ê·) â‰¡ 13
resultâ‚ƒ = refl , refl , refl , refl

\end{code}

tâ‚„ is taken from [Esc13] in which is named tâ‚ƒ.

\begin{code}

tâ‚„ : T Îµ ((Î¹ â‡¾ Î¹) â‡¾ Î¹)
tâ‚„ = Lam (Rec Â· (Î½â‚€ Â· Num 1) Â· (Lam Î½â‚) Â· (Plus (Î½â‚€ Â· Num 2) (Î½â‚€ Â· Num 3)))

tâ‚„-interpretation : âŸ¦ tâ‚„ âŸ§ â‰¡ Î» Î± â†’ rec (Î± 1) (Î» _ â†’ Î±) (Î± 2 + Î± 3)
tâ‚„-interpretation = refl

resultâ‚„ :  M tâ‚„ 0Ê· â‰¡ 4
        Ã—  M tâ‚„ Î´ â‰¡ 4
        Ã—  M tâ‚„ succ â‰¡ 9
        Ã—  M tâ‚„ (12 âœ¶ 67 âœ¶ 8 âœ¶ 99 âœ¶ 3 âœ¶ 0Ê·) â‰¡ 68
resultâ‚„ = refl , refl , refl , refl

\end{code}

tâ‚… is also taken from [Esc13]. The evaluations of this complicated
example are instantaneous.

\begin{code}

tâ‚… : T Îµ ((Î¹ â‡¾ Î¹) â‡¾ Î¹)
tâ‚… = Lam (Rec Â· (Î½â‚€ Â· (Rec Â· (Î½â‚€ Â· (Rec Â· (Î½â‚€ Â· Num 17) Â· Lam Î½â‚ Â· (Î½â‚€ Â· Num 17)))
                           Â· Lam Î½â‚
                           Â· (Rec Â· (Plus (Î½â‚€ Â· (Î½â‚€ Â· Num 2)) (Î½â‚€ Â· Num 3))
                                  Â· Lam Î½â‚
                                  Â· (Rec Â· (Î½â‚€ Â· Num 1) Â· Lam Î½â‚ Â· (Plus (Î½â‚€ Â· Num 2) (Î½â‚€ Â· Num 3))))))
              Â· (Lam Î½â‚)
              Â· (Î½â‚€ Â· Num 2))

tâ‚…-interpretation : âŸ¦ tâ‚… âŸ§ â‰¡ Î» Î± â†’ rec (Î± (rec (Î± (rec (Î± 17) (Î» _ â†’ Î±) (Î± 17)))
                                               (Î» _ â†’ Î±)
                                               (rec (Î± (Î± 2) + Î± 3)
                                                    (Î» _ â†’ Î±)
                                                    (rec (Î± 1) (Î» _ â†’ Î±) (Î± 2 + Î± 3)))))
                                       (Î» _ â†’ Î±)
                                       (Î± 2)
tâ‚…-interpretation = refl

resultâ‚… :  M tâ‚… 0Ê· â‰¡ 18
        Ã—  M tâ‚… Î´ â‰¡ 18
        Ã—  M tâ‚… succ â‰¡ 58
        Ã—  M tâ‚… (12 âœ¶ 67 âœ¶ 8 âœ¶ 99 âœ¶ 3 âœ¶ 0Ê·) â‰¡ 68
resultâ‚… = refl , refl , refl , refl

\end{code}
